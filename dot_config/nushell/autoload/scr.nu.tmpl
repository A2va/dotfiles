
def show_color [color: string, msg: string] {
    let colors = {
        red: "\e[31m"
        blue: "\e[36m"
    }
    let reset = "\e[0m"
    print $"($colors | get $color)($msg)($reset)"
}

def --env scr [
  language?: string,
  name?: string
] {
    let version = "v7"
    let supported = [rust c cpp java gradle python scala md]
    let java_version = 21

    mut language = $language

    if $language == "-v" {
        print $"Nushell script ($version) installed."
        return
    }

    if 'SCR_TMP_FOLDER' not-in $env  {
        show_color red "Error: You must define SCR_TMP_FOLDER as a valid directory."
        show_color blue 'Example: $env.SCR_TMP_FOLDER = "/tmp/scratch_projects"'
        return
    }

    if ($language | is-empty) {
        print "Choose a language:"
        $language = $supported | input list "Select Language:"
        if ($language | is-empty) { 
            return 
        }
    }

    if not ($language in $supported) {
        show_color red $"Error: '($language)' is not supported. Choose one of: ($supported | str join ', ')"
        return
    }

    mut project_name = $name
    let use_custom_name = if ($name | is-empty) == false {
        if (ls $name | is-empty) == false {
            show_color red $"Path '($name)' already exists. Aborting to prevent overwrite."
            return
        }
        true
    } else {
        let timestamp = (date now | format date "%Y-%m-%d-%H%M%S")
        let name_gen = $"scratch-($language)-($timestamp)"
        $project_name = $name_gen
        false
    }

    if not $use_custom_name {
        mkdir $env.SCR_TMP_FOLDER
        cd $env.SCR_TMP_FOLDER
    }

    show_color blue $"Creating scratch project for '($language)' in folder '($project_name)'..."

    mut entry = ""

    match $language {
        "rust" => {
            ^cargo new $project_name
            cd $project_name
            $entry = "src/main.rs"
        }
        "md" => {
            $entry = $"($project_name).md"
            "# Markdown Scratch File" | save --force $entry
        }
        "c" => {
            mkdir $project_name
            cd $project_name
            $entry = "main.c"
            '{{ template "scr/main.c.tmpl" . }}' | save --force $entry
        }
        "cpp" => {
            mkdir $project_name
            cd $project_name
            $entry = "main.cpp"
            '{{ template "scr/main.cpp.tmpl" . }}' | save --force $entry
        }
        "java" => {
            mkdir $project_name
            cd $project_name
            $entry = "App.java"
            '{{ template "scr/App.java.tmpl" . }}' | save --force $entry
        }
        "python" => {
            mkdir $project_name
            cd $project_name
            $entry = "main.py"
            '{{ template "scr/main.py.tmpl" . }}' | save --force $entry
        }
        "gradle" => {
            mkdir $project_name
            cd $project_name
            (
                ^gradle init
                --type java-application
                --dsl kotlin
                --test-framework junit-jupiter
                --package scratch
                --project-name scratch
                --no-split-project
                --no-incubating
                --java-version $java_version
            )
            $entry = "app/src/main/java/scratch/App.java"
        }
        "scala" => {
            mkdir $project_name
            cd $project_name
            $entry = "Main.scala"
            '{{ template "scr/Main.scala.tmpl" . }}' | save --force $entry
            '{{ template "scr/scalafmt.conf.tmpl" . }}' | save --force ".scalafmt.conf"
        }
    }

    if 'SCR_EDITOR' not-in $env  {
        show_color red "Scratch project created, but SCR_EDITOR is not set."
        show_color blue 'Example: $env.SCR_EDITOR = "code .; code"'
    } else {
        ^($env.SCR_EDITOR) $entry
    }
}
