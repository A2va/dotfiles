
module utils {
    export const VERSION = "v9"

    export def color_print [color: string, msg: string] {
        print ($"(ansi $color)($msg)(ansi reset)")
    }
}
use utils *

# scr v9 - Generate scratch project in various languages based on simple templates or generators
# Configure scr with SCR_TMP_FOLDER and SCR_EDITOR
#  `$env.SCR_TMP_FOLDER = ~/path/somewhere`
#  `$env.SCR_EDITOR = "code . and code "`
# Pick the given template
#   `scr rust`     - Create a new Cargo project
#   `scr c`        - Hello World in C
#   `scr cpp`      - Hello World in C++
#   `scr pco`      - PCO program using pcosynchro
#   `scr python`   - Hello World in Python
#   `scr java`     - Hello World in App.java
#   `scr gradle`   - Java Gradle project (gradle init)
#   `scr scala`    - Hello World in Main.scala
def --env scr [
  language?: string,
  name?: string
  --version(-v)            # Show version
] {

    let supported = [rust c cpp java gradle python scala md]
    let java_version = 21

    mut language = $language

    if $version {
        print $"Nushell script ($VERSION) installed."
        return
    }

    if 'SCR_TMP_FOLDER' not-in $env  {
        color_print red "Error: You must define SCR_TMP_FOLDER as a valid directory."
        color_print blue 'Example: $env.SCR_TMP_FOLDER = "/tmp/scratch_projects"'
        return
    }

    if ($language | is-empty) {
        $language = $supported | input list "Select Language"
        if ($language | is-empty) { 
            return 
        }
    }

    if not ($language in $supported) {
        color_print red $"Error: '($language)' is not supported. Choose one of: ($supported | str join ', ')"
        return
    }

    let base_folder = $env.SCR_TMP_FOLDER

    mut project_name = $name
    if ($name | is-empty) == false {
        if (ls $name | is-empty) == false {
            color_print red $"Path '($name)' already exists. Aborting to prevent overwrite."
            return
        }
    } else {
        let timestamp = (date now | format date "%Y-%m-%d-%H%M%S")
        let name_gen = $"scratch-($language)-($timestamp)"
        $project_name = $name_gen
    }

    color_print blue $"Creating scratch project for '($language)' in folder '($project_name)'..."
    let final = $"($project_name)/($project_name)"
    mut entry = ""

    match $language {
        "rust" => {
            mkdir $final; cd $final
            ^cargo init --vcs none
            $entry = "src/main.rs"
        }
        "md" => {
            cd $base_folder
            $entry = $"($project_name).md"
            touch $entry
        }
        "c" => {
            mkdir $final; cd $final
            xmake create -l c -P .
            $entry = "src/main.c"
        }
        "cpp" => {
            mkdir $final; cd $final
            xmake create -l c++ -P .
            $entry = "src/main.cpp"
        }
        "java" => {
            mkdir $final; cd $final
            $entry = "App.java"
            '{{ template "scr/App.java.tmpl" . }}' | save --force $entry
        }
        "python" => {
            mkdir $final; cd $final
            $entry = "main.py"
            '{{ template "scr/main.py.tmpl" . }}' | save --force $entry
        }
        "gradle" => {
            mkdir $final and cd $final
            (
                ^gradle init
                --type java-application
                --dsl kotlin
                --test-framework junit-jupiter
                --package scratch
                --project-name scratch
                --no-split-project
                --no-incubating
                --java-version $java_version
            )
            $entry = "app/src/main/java/scratch/App.java"
        }
        "scala" => {
            mkdir $final; cd $final
            $entry = "Main.scala"
            '{{ template "scr/Main.scala.tmpl" . }}' | save --force $entry
            '{{ template "scr/scalafmt.conf.tmpl" . }}' | save --force ".scalafmt.conf"
        }
    }

    if 'SCR_EDITOR' not-in $env  {
        color_print red "Scratch project created, but SCR_EDITOR is not set."
        color_print blue 'Example: $env.SCR_EDITOR = "code"'
    } else {
        ^($env.SCR_EDITOR) $entry
    }
}
